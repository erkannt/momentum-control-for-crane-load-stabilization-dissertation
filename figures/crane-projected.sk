## Double pointmass pendulum using projected coordinates
##

input{./sketch-lib/defaults.sk}

def L1 5
def J1 -15
def L2 3
def J2 -15

def arrowarc
{
put{scale(0.5)}{
    sweep [style=thin]{215, rotate(1, [Z])} (1,0)
    def p1 (1,0) then rotate(210, [Z])
    def p2 (1,0) then rotate(210+30, [Z])
    line [style=thin, style=->] (p1)(p2)
}
}

% The main drawing
put {scale(1)}
{
    def style [cull=false, fill=lightgray]
        input{./sketch-lib/objects.sk}
    # base
    put {scale(1)}
    {
        put{scale([2,2,0.5])}{box}
        put{scale([3,3,-0.1]) * translate([0,0,0])}
        {
            {coordsys}
            special |\path #2 node[below] {$X_0$}
                           #3 node[right] {$Y_0$};|(z)(x)(y)(o)
        }


        # mass 1
        put {translate([2, 2, -L1])} {
            put{scale([0.1,0.1,0.1])}{sphere}

            # coordsys 1
            put{scale([1.5,1.5,1.5]) * translate([0,0,0])}
            {
                {coordsys}
                special |\path #1 node[right] {$Z_1$}
                    #2 node[below] {$X_1$}
                    #3 node[right] {$Y_1$};|(z)(x)(y)(o)
            }

            # lower assembly
            {
                put {translate([1, 1, -L2]) * rotate(-18.43, [Y]) * rotate(18.43, [X])} {
                    # mass 2
                    put{scale([0.6,0.6,0.6])}{box}

                    # theta_23
                    line [style=dashed] (0,0,0)(0,0,-1.5)
                    put {translate([0,0,-1.5]) * rotate(100, [Z])}{arrowarc}
                    special|\node[right] at #1 {$\theta_{23}$};|(-1.3,0,-1.2)
                }

                # coordsys 2
                put {translate([1, 1, -L2])} {
                    put{scale([1.5,1.5,1.5]) * translate([0,0,0])}
                    {
                        {coordsys}
                        special |\path #1 node[right] {$Z_2$}
                            #2 node[below] {$X_2$}
                            #3 node[right] {$Y_2$};|(z)(x)(y)(o)
                    }
                }
                # link2
                line [style=solid] (0,0,0)(1,1,-L2)
                special|\node[right] at #1 {$l_2$};|(0.5,0.5,-L2*0.5)

                # annotation 2
                line [style=dashed] (0,0,0)(0,0,-L2)
                line [style=dashed] (0,0,0)(0,1,-L2)
                line [style=dashed] (0,0,0)(1,0,-L2)
                line [style=dashed] (0,0,-L2)(1,0,-L2)
                line [style=dashed] (0,0,-L2)(0,1,-L2)
                line [style=dashed] (1,0,-L2)(1,1,-L2)
                line [style=dashed] (0,1,-L2)(1,1,-L2)

                # theta_21
                put {rotate(90-18.43, [Y])}{
                    sweep [style=thin, style=<-]{18.43, rotate(1, [Y])} (2.5,0)
                }
                special|\node[right] at #1 {$\theta_{21}$};|(-1,0,-2.5)
                
                # theta_22
                put {rotate(90, [Z]) * rotate(90-18.43, [Y])}{
                    sweep [style=thin, style=<-]{18.43, rotate(1, [Y])} (2,0)
                }
                special|\node[right] at #1 {$\theta_{22}$};|(-1,0,-2)
            }
        }
        # link1
        line [style=solid] (0,0,0)(2,2,-L1)
        special|\node[right] at #1 {$l_1$};|(1,1,-L1*0.5)

        # annotation 1
        line [style=dashed] (0,0,0)(0,0,-L1)
        line [style=dashed] (0,0,0)(0,2,-L1)
        line [style=dashed] (0,0,0)(2,0,-L1)
        line [style=dashed] (0,0,-L1)(2,0,-L1)
        line [style=dashed] (0,0,-L1)(0,2,-L1)
        line [style=dashed] (2,0,-L1)(2,2,-L1)
        line [style=dashed] (0,2,-L1)(2,2,-L1)

        # theta_1
        put {rotate(90-21.8, [Y])}{
            sweep [style=thin, style=<-]{21.8, rotate(1, [Y])} (4.5,0)
        }
        special|\node[right] at #1 {$\theta_{11}$};|(-1,0,-4.5)
        
        # theta_2
        put {rotate(90, [Z]) * rotate(90-21.8, [Y])}{
            sweep [style=thin, style=<-]{21.8, rotate(1, [Y])} (3,0)
        }
        special|\node[right] at #1 {$\theta_{12}$};|(-1,0,-3)
    }
}



global
{
    language tikz
    camera rotate(0, (1,0,0)) * view((1.5,2,1),(0,0,0),[0,0,1]) * rotate(110, (0,0,1))
}

