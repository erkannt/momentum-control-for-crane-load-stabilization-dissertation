## Double pointmass pendulum using projected coordinates
##

input{./sketch-lib/defaults.sk}

def hash {
    line[style=thin] (o)(0.2,0.2)
}

def orth {
    put{scale(0.3)}{
        line (0,1)(1,1)
        line (1,0)(1,1)
    }
}

% left half of drawing
put {scale(1)}
{
    def L1 5
    def J1 -30
    def L2 3
    def J2 30

    def style [cull=false, fill=lightgray]
        input{./sketch-lib/objects.sk}

    put {scale(1)}
    {
        # base
        put{scale([1.5,1.5,1.5]) * translate([0,0,0])}
        {
            line [style=thick, style=->] (o)(x)
            line [style=thick, style=->] (o)(y)
            special |\path
                #1 node[below] {$X$}
                #2 node[right] {$Y$};|(x)(y)(o)
        }
        put{scale([0.1,0.1,0.1])}{
            def style [fill=black, style=thick]
            input{./sketch-lib/cylinder}
            }
        put{translate([-1,0,0])}{
            line [style=thick] (o)(2,0)
            repeat {2/0.2+1, translate([0.2,0,0])} {hash}
        }
        line [style=dashed] (o)(0,-L1)
        put{rotate(-90-30, [Z])}{
            {
                def sweepangle 30
                def sweepdist 2.5
                input{./sketch-lib/arrowarc}
                def p1 (sweepdist,0)
                def p2 (sweepdist,0) then rotate(sweepangle, [Z])
                special|\path  #1 -- node[above, sloped]{$\theta_1$} #2;|(p1)(p2)
            }
        }


        # mass 1 coordsys
        put {rotate(J1, [Z]) * translate([0, -L1, 0])}{
            # link1
            line (o)(0, L1, 0)
            # mass 1
            put{scale(0.2)}{cylinder}
            # F1
            line [style=thick, style=->] (o)(2,0)
            special|\node[above] at #1 {$F_1$};|(2,0)
            {orth}

            #mass 2 ref line
            put {rotate(-J1, [Z])}{
                line [style=dashed] (o)(0,-L2)
                put{rotate(-90, [Z])}{
                    {
                        def sweepangle 30
                        def sweepdist 2 
                        input{./sketch-lib/arrowarc}
                        def p1 (sweepdist,0)
                        def p2 (sweepdist,0) then rotate(sweepangle, [Z])
                        special|\path  #1 -- node[above, sloped]{$\theta_2$} #2;|(p1)(p2)
                    }
                }
            }

            # mass 2 coordsys
            put {rotate(J2-J1, [Z]) * translate([0, -L2, 0])}{
                # link2
                line (o)(0, L2, 0)
                # mass 2
                put{scale(0.2)}{cylinder}
                # F2
                line [style=thick, style=->] (o)(2,0)
                special|\node[above] at #1 {$F_2$};|(2,0)
                {orth}
            }
        }
    }
}

% right half of drawing
def L1 4.3
def F1 1.6
def F2 4.7
def J1 40
def J2 30
put {scale(1) * translate([L1+4,-L1+1,0])}{
    line [style=dashed] (0,-L1)(0,L1)
    line [style=dashed] (-L1,0)(L1,0)

    # F1 coordinates
    put {rotate(90-J1, [Z])}{
        {orth}
        #F1 orth
        line (o)(5,0)
        #F1
        line (o)(0,5)
        line [style=thick, style=->] (o)(0,F1)
        special|\node[right] at #1 {$F_1$};|(0,F1*0.6)

        #Triangle corner
        put {translate([0,F1,0])}{
            line [style=dashed] (o)(-5,0)
            put {rotate(180, [Z])}{orth}
        }
    }
    # F2
    put{rotate(J2, [Z])}{
        line [style=thick, style=->] (o)(-F2,0)
        special|\node[below] at #1 {$F_2$};|(-F2*0.5,0)
    }
    # theta1
    put{rotate(90-J1, [Z])}{
        {
            def sweepangle 40
            def sweepdist 2 
            input{./sketch-lib/arrowarc}
            def p1 (sweepdist*0.8,0)
            def p2 (sweepdist*0.8,0) then rotate(sweepangle, [Z])
            special|\path  #1 -- node{$\theta_1$} #2;|(p1)(p2)
        }
    }
    # theta1
    put{rotate(180-J1, [Z])}{
        {
            def sweepangle 40
            def sweepdist 4 
            input{./sketch-lib/arrowarc}
            def p1 (sweepdist*1.2,0)
            def p2 (sweepdist*1.2,0) then rotate(sweepangle, [Z])
            special|\path  #1 -- node{$\theta_1$} #2;|(p1)(p2)
        }
    }
    # theta1 + theta2
    put{rotate(180-J1, [Z])}{
        {
            def sweepangle 70
            def sweepdist 3.5 
            input{./sketch-lib/arrowarc}
            def p1 (sweepdist*0.8,0)
            def p2 (sweepdist*0.8,0) then rotate(sweepangle, [Z])
            special|\path  #1 -- node[pos=0.2]{$\theta_1+\theta_2$} #2;|(p1)(p2)
        }
    }
}

global
{
    language tikz
}

